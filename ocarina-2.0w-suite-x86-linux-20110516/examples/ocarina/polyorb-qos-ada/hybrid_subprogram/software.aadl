data message
properties 
  Data_Model::Data_Representation => integer;
end message;

--  subprograms

subprogram transmit_message
features
  msg_in : in parameter message;
  msg_out : out parameter message;
properties
  source_language => Ada95;
  source_name => "Repository.Transmit_Message";
end transmit_message;

subprogram send_message
features
  msg_out : out parameter message;
properties
  source_language => Ada95;
  source_name => "Repository.Send_Message";
end send_message;

subprogram receive_message
features
  msg_in : in parameter message;
properties
  source_language => Ada95;
  source_name => "Repository.Receive_Message";
end receive_message;

subprogram make_all --  test hybrid subprogram
features
  msg_in : in parameter message;
  msg_out : out parameter message;
properties
  source_language => Ada95;
end make_all;

subprogram call_seq --  test pure sequence call
features
  msg_in : in parameter message;
  msg_out : out parameter message;
end call_seq;

subprogram implementation make_all.i
calls kolz : {
  sp1 : subprogram transmit_message;
  sp2 : subprogram transmit_message;
};
connections
  parameter msg_in -> sp1.msg_in;
  parameter sp1.msg_out -> sp2.msg_in;
  parameter sp2.msg_out -> msg_out;
properties
    source_name => "Repository_2.Make_All_I";
end make_all.i;

subprogram implementation call_seq.i
calls kolz : {
  sp1 : subprogram transmit_message;
  sp2 : subprogram transmit_message;
};
connections
  parameter msg_in -> sp1.msg_in;
  parameter sp1.msg_out -> sp2.msg_in;
  parameter sp2.msg_out -> msg_out;
end call_seq.i;

--  sender threads

thread sender
features
  msg_out : out event data port message;
properties
  Dispatch_Protocol => Periodic;
  Period => 1000 Ms;
end sender;

thread implementation sender.simple
calls {
  sp1 : subprogram send_message;
};
connections
  parameter sp1.msg_out -> msg_out;
end sender.simple;

--  receiver threads

thread receiver
features
  msg_in : in event data port message;
properties
  Dispatch_Protocol => Aperiodic;
end receiver;

thread implementation receiver.simple
calls {
  sp1 : subprogram receive_message;
};
connections
  parameter msg_in -> sp1.msg_in;
end receiver.simple;

--  transmitter threads

thread transmitter
features
  msg_in : in event data port message;
  msg_out : out event data port message;
properties
  Dispatch_Protocol => Aperiodic;
end transmitter;

thread implementation transmitter.simple1
calls {
  sp1 : subprogram make_all.i;
  };
connections
  parameter msg_in -> sp1.msg_in;
  parameter sp1.msg_out -> msg_out;
end transmitter.simple1;

thread implementation transmitter.simple2
calls {
  sp1 : subprogram call_seq.i;
  };
connections
  parameter msg_in -> sp1.msg_in;
  parameter sp1.msg_out -> msg_out;
end transmitter.simple2;

--  transmitter processes

process transmitter_node
features
  msg_in : in event data port message;
  msg_out : out event data port message;
end transmitter_node;

process implementation transmitter_node.hybrid
subcomponents
  th1 : thread transmitter.simple1;
connections
  event data port msg_in -> th1.msg_in;
  event data port th1.msg_out -> msg_out;
end transmitter_node.hybrid;

--  sender processes
process implementation transmitter_node.sequence
subcomponents
  th1 : thread transmitter.simple2;
connections
  event data port msg_in -> th1.msg_in;
  event data port th1.msg_out -> msg_out;
end transmitter_node.sequence;

process sender_node
features
  msg_in : in event data port message;
  msg_out : out event data port message;
end sender_node;

process implementation sender_node.simple
subcomponents
  th1 : thread sender.simple;
  th2 : thread receiver.simple;
connections
  event data port msg_in -> th2.msg_in;
  event data port th1.msg_out -> msg_out;
end sender_node.simple;
