data message
properties 
  Data_Model::Data_Representation => integer;
end message;

--  subprograms

subprogram send_broadcast_msg
features
  msg_out : out parameter message;
properties
  source_language => Ada95;
  source_name => "Repository.Send_Broadcast_Msg";
end send_broadcast_msg;

subprogram send_message_1
features
  msg_in : in parameter message;
  msg_out : out parameter message;
properties
  source_language => Ada95;
  source_name => "Repository.Send_Message_1";
end send_message_1;

subprogram send_message_2
features
  msg_in : in parameter message;
  msg_out : out parameter message;
properties
  source_language => Ada95;
  source_name => "Repository.Send_Message_2";
end send_message_2;

subprogram print_received_msg
features
  msg_in : in parameter message;
properties
  source_language => Ada95;
  source_name => "Repository.Print_Received_Msg";
end print_received_msg;

subprogram dummy
properties
  source_language => Ada95;
  source_name => "Repository.Dummy";
end dummy;

-- autist thread

thread autist
properties
  Dispatch_Protocol => Periodic;
  Period => 1000 Ms;
end autist;

thread implementation autist.impl
calls {
  sp1 : subprogram dummy;
};
end autist.impl;

-- receive_replies threads

thread receive_replies
features
  msg_in : in event data port message;
properties
  Dispatch_Protocol => Aperiodic;
end receive_replies;

thread implementation receive_replies.impl
calls {
  sp1 : subprogram print_received_msg;
};
connections
  parameter msg_in -> sp1.msg_in;
properties
  Deployment::Priority => 1;
end receive_replies.impl;

--  sender threads

thread sender
features
  msg_out : out event data port message;
properties
  Dispatch_Protocol => Periodic;
  Period => 1000 Ms;
end sender;

thread implementation sender.impl
calls {
  sp1 : subprogram send_broadcast_msg;
};
connections
  parameter sp1.msg_out -> msg_out;
properties
  Deployment::Priority => 1;
end sender.impl;

--  receiver threads

thread receiver
features
  msg_in : in event data port message;
  msg_out : out event data port message;
properties
  Dispatch_Protocol => Aperiodic;
end receiver;

thread implementation receiver.impl1
calls {
  sp1 : subprogram send_message_1;
};
connections
  parameter msg_in -> sp1.msg_in;
  parameter sp1.msg_out -> msg_out;
properties
  Deployment::Priority => 1;
end receiver.impl1;

thread implementation receiver.impl2
calls {
  sp1 : subprogram send_message_2;
};
connections
  parameter msg_in -> sp1.msg_in;
  parameter sp1.msg_out -> msg_out;
properties
  Deployment::Priority => 28;
end receiver.impl2;

--  receiver processes

process receiver_node
features
  msg_in : in event data port message;
  msg_out : out event data port message;
end receiver_node;

process implementation receiver_node.impl1
subcomponents
  th1 : thread receiver.impl1;
  th2 :	thread autist.impl;
connections
  event data port msg_in -> th1.msg_in;
  event data port th1.msg_out -> msg_out;
end receiver_node.impl1;

process implementation receiver_node.impl2
subcomponents
  th1 : thread receiver.impl2;
  th2 :	thread autist.impl;
connections
  event data port msg_in -> th1.msg_in;
  event data port th1.msg_out -> msg_out;
end receiver_node.impl2;

--  sender processes

process sender_node
features
  msg_in_1 : in event data port message;
  msg_in_2 : in event data port message;
  msg_out  : out event data port message;
end sender_node;

process implementation sender_node.impl
subcomponents
  th1 : thread sender.impl;
  th2 : thread receive_replies.impl;
connections
  event data port msg_in_1 -> th2.msg_in;
  event data port msg_in_2 -> th2.msg_in;
  event data port th1.msg_out -> msg_out;
end sender_node.impl;
