---------------
-- Processor --
---------------

processor the_processor
features
  ETH : requires bus access Ethernet_Bus;
properties
  Deployment::location           => "127.0.0.1";
  Deployment::Execution_Platform => Native;
end the_processor;

---------
-- Bus --
---------

bus Ethernet_Bus
properties
  Deployment::Transport_API => BSD_Sockets;
end Ethernet_Bus;

---------------
-- Processes --
---------------

process A
features
  Out_Boolean : out event data port Boolean_Type;
  Out_Integer : out event data port Integer_Type;
  Out_Struct  : out event data port Struct_Type.Impl;
end A;

process implementation A.Impl
subcomponents
  Emitter_Thread : thread Emitter.Impl;
connections
  event data port Emitter_Thread.Boolean_Source -> Out_Boolean;
  event data port Emitter_Thread.Integer_Source -> Out_Integer;
  event data port Emitter_Thread.Struct_Source -> Out_Struct;
end A.Impl;

process B
features
  In_Boolean  : in event data port Boolean_Type;
  In_Integer  : in event data port Integer_Type;
  In_Struct   : in event data port Struct_Type.Impl;
end B;

process implementation B.Impl
subcomponents
  Boolean_Rcv_Thread : thread Boolean_Receiver.Impl;
  Integer_Rcv_Thread : thread Integer_Receiver.Impl;
  Struct_Rcv_Thread  : thread Struct_Receiver.Impl;
connections
  event data port In_Boolean -> Boolean_Rcv_Thread.Data_Sink;
  event data port In_Integer -> Integer_Rcv_Thread.Data_Sink;
  event data port In_Struct  -> Struct_Rcv_Thread.Data_Sink;
end B.Impl;

------------
-- System --
------------

system Some_Types
end Some_Types;

system implementation Some_Types.Impl
subcomponents
  Node_A : process A.Impl;
  Node_B : process B.Impl {Deployment::port_number => 12002;};

  CPU : processor the_processor;
  the_bus : bus Ethernet_Bus;
connections
  bus access the_bus -> CPU.ETH;
  event data port Node_A.Out_Boolean -> Node_B.In_Boolean
    {Actual_Connection_Binding => (reference the_bus);};
  event data port Node_A.Out_Integer -> Node_B.In_Integer
    {Actual_Connection_Binding => (reference the_bus);};
  event data port Node_A.Out_Struct -> Node_B.In_Struct
    {Actual_Connection_Binding => (reference the_bus);};
properties
  actual_processor_binding => reference CPU applies to Node_A;
  actual_processor_binding => reference CPU applies to Node_B;
end Some_Types.Impl;
